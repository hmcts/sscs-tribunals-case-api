plugins {
    id 'application'
//    id 'checkstyle'
    id 'pmd'
    id 'org.sonarqube' version '2.5'
    id 'jacoco'
    id 'io.spring.dependency-management' version '1.0.4.RELEASE'
    id 'org.springframework.boot' version '1.5.9.RELEASE'
    id 'org.owasp.dependencycheck' version '3.1.0'
    id 'com.github.ben-manes.versions' version '0.17.0'
}

group = 'uk.gov.hmcts.reform'
version = '1.0.0'

sourceCompatibility = 1.8
targetCompatibility = 1.8

//checkstyle {
//    maxWarnings = 0
//    toolVersion = '8.7'
//    // need to set configDir to rootDir otherwise submodule will use submodule/config/checkstyle
//    configDir = new File(rootDir, 'config/checkstyle')
//}

pmd {
    toolVersion = "5.8.1"
    ignoreFailures = true
    sourceSets = [sourceSets.main, sourceSets.test]
    reportsDir = file("$project.buildDir/reports/pmd")
    ruleSetFiles = files("config/pmd/ruleset.xml")
}

dependencyUpdates.resolutionStrategy = {
    componentSelection { rules ->
        rules.all { ComponentSelection selection ->
            boolean rejected = ['alpha', 'beta', 'rc', 'cr', 'm'].any { qualifier ->
                selection.candidate.version ==~ /(?i).*[.-]${qualifier}[.\d-]*/
            }
            if (rejected) {
                selection.reject('Release candidate')
            }
        }
    }
}

// https://jeremylong.github.io/DependencyCheck/dependency-check-gradle/configuration.html
dependencyCheck {
    // Specifies if the build should be failed if a CVSS score above a specified level is identified.
    // range of 0-10 fails the build, anything greater and it doesn't fail the build
    failBuildOnCVSS = System.getProperty('dependencyCheck.failBuild') == 'true' ? 0 : 11
    suppressionFile = 'dependency-check-suppressions.xml'
}

def springBootVersion = plugins.getPlugin('org.springframework.boot').class.package.implementationVersion

repositories {
    maven {
        url "https://dl.bintray.com/hmcts/hmcts-maven"
    }
    jcenter()
    mavenCentral()
}

configurations {
    integrationTestCompile.extendsFrom(testCompile)
    integrationTestRuntime.extendsFrom(testRuntime)
}

def versions = [
        springfoxSwagger: '2.7.0'
]

dependencies {
    compile group: 'org.springframework.boot', name: 'spring-boot-starter', version: springBootVersion
    compile group: 'org.springframework.boot', name: 'spring-boot-starter-web', version: springBootVersion
    compile group: 'org.springframework.boot', name: 'spring-boot-starter-actuator', version: springBootVersion
    compile group: 'org.springframework.boot', name: 'spring-boot-starter-aop', version: springBootVersion
    compile group: 'org.springframework.boot', name: 'spring-boot-starter-mail', version: springBootVersion
    compile group: 'org.springframework.retry', name: 'spring-retry'
    compile group: 'com.googlecode.json-simple', name: 'json-simple', version: '1.1.1'
    compile group: 'com.google.guava', name: 'guava', version: '18.0'

    compile group: 'com.fasterxml.jackson.core', name: 'jackson-databind'
    compile group: 'com.fasterxml.jackson.module', name: 'jackson-module-parameter-names'
    compile group: 'com.fasterxml.jackson.datatype', name: 'jackson-datatype-jdk8'
    compile group: 'com.fasterxml.jackson.datatype', name: 'jackson-datatype-jsr310'

    compile group: 'org.json', name: 'json', version: '20140107'
    compile group: 'net.objectlab.kit', name: 'datecalc-common', version: '1.4.0'
    compile group: 'net.objectlab.kit', name: 'datecalc-jdk8', version: '1.4.0'

    compile('io.springfox:springfox-swagger2:2.7.0')
    compile('io.springfox:springfox-swagger-ui:2.7.0')

    compile 'com.migesok:jaxb-java-time-adapters:1.1.3'

    compile group: 'commons-io', name: 'commons-io', version: '2.6'

    compile group: 'uk.gov.hmcts.reform', name: 'java-logging-spring', version: '1.4.0'
    compile group: 'uk.gov.hmcts.reform', name: 'java-logging-httpcomponents', version: '1.4.0'
    compile group: 'uk.gov.hmcts.reform', name: 'http-proxy-spring-boot-autoconfigure', version: '1.1.0'
    compile group: 'uk.gov.hmcts.reform', name: 'pdf-service-client', version: '5.0.0'
    compile group: 'uk.gov.hmcts.reform', name: 'service-auth-provider-client', version: '0.4.0'

    compile group: 'org.apache.commons', name: 'commons-text', version: '1.2'

    testCompile group: 'org.springframework.boot', name: 'spring-boot-starter-test', version: springBootVersion
    testCompile group: 'org.mockito', name: 'mockito-core', version: '1.10.19'
    testCompile group: 'org.hamcrest', name: 'hamcrest-core', version: '1.3'

}

sourceSets {
    integrationTest {
        java {
            compileClasspath += main.output + test.output
            runtimeClasspath += main.output + test.output
            srcDir('src/IntegrationTests/java')
        }
        resources {
            srcDir('src/IntegrationTests/resources')
        }
    }
}

dependencies {
    integrationTestCompile(sourceSets.test.output)
}

jar {
    archiveName 'tribunals-case-api.jar'
    manifest {
        attributes 'Implementation-Title': project.name,
                'Implementation-Version': project.version
    }
}

sonarqube {
    properties {
        property "sonar.projectName", "SSCS - Tribunals Case Api"
        property "sonar.exclusions", "src/main/java/uk/gov/hmcts/sscs/domain/**," +
                "src/main/java/uk/gov/hmcts/sscs/TribunalsCaseApiApplication.java," +
                "src/main/java/uk/gov/hmcts/sscs/email/Email.java," +
                "src/main/java/uk/gov/hmcts/sscs/config/*," +
                "src/main/java/uk/gov/hmcts/sscs/model/*"
    }
}
