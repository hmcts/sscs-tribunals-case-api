plugins {
    id 'application'
    id 'checkstyle'
    id 'pmd'
    id 'org.sonarqube' version '2.6.2'
    id 'jacoco'
    id 'org.springframework.boot' version '2.0.5.RELEASE'
    id 'org.owasp.dependencycheck' version '3.2.1'
    id 'com.github.ben-manes.versions' version '0.17.0'
    id 'net.ltgt.apt' version '0.13'
    id "info.solidsoft.pitest" version '1.3.0'
}

apply plugin: 'org.springframework.boot'
apply plugin: 'io.spring.dependency-management'

group = 'uk.gov.hmcts.reform'
version = '1.0.0'

sourceCompatibility = 1.8
targetCompatibility = 1.8

compileJava {
    options.compilerArgs << '-parameters' << '-Xlint:deprecation' << "-Xlint:unchecked"
}

compileTestJava {
    options.compilerArgs << '-Xlint:deprecation'
}

mainClassName = 'uk.gov.hmcts.reform.sscs.TribunalsCaseApiApplication'

check.dependsOn dependencyCheckAnalyze

checkstyle {
    maxWarnings = 0
    toolVersion = '8.7'
    // need to set configDir to rootDir otherwise submodule will use submodule/config/checkstyle
    configDir = new File(rootDir, 'config/checkstyle')
}

pmd {
    toolVersion = "5.8.1"
    ignoreFailures = true
    sourceSets = [sourceSets.main, sourceSets.test]
    reportsDir = file("$project.buildDir/reports/pmd")
    ruleSetFiles = files("config/pmd/ruleset.xml")
}

sourceSets {
    integrationTest {
        java {
            compileClasspath += main.output + test.output
            runtimeClasspath += main.output + test.output
            srcDir('src/IntegrationTests/java')
        }
        resources {
            srcDir('src/IntegrationTests/resources')
        }
    }
    e2e {
        java {
            compileClasspath += main.output + test.output
            runtimeClasspath += main.output + test.output
            srcDir('src/e2e/java')
        }
    }
}

dependencies {
    integrationTestCompile(sourceSets.test.output)
    e2eCompile(sourceSets.test.output)
}

task integration(type: Test) {
    setTestClassesDirs(sourceSets.integrationTest.output.classesDirs)
    setClasspath(sourceSets.integrationTest.runtimeClasspath)
}

task functional(type: Test) {
    group = 'Functional Tests'
    description = 'Executes non-destructive functional tests in AAT against a running CCD and notification service'
    setTestClassesDirs(sourceSets.e2e.output.classesDirs)
    setClasspath(sourceSets.e2e.runtimeClasspath)
    include "uk/gov/hmcts/sscs/functional/**"
    exclude "uk/gov/hmcts/sscs/smoke/**"
}

jacoco {
    toolVersion = "0.8.1"
}

jacocoTestReport {
    executionData(test, integration)
    reports {
        xml.enabled = true
        csv.enabled = false
        xml.destination file("${project.buildDir}/reports/jacoco/test/jacocoTestReport.xml")
    }
}

dependencyUpdates.resolutionStrategy = {
    componentSelection { rules ->
        rules.all { ComponentSelection selection ->
            boolean rejected = ['alpha', 'beta', 'rc', 'cr', 'm'].any { qualifier ->
                selection.candidate.version ==~ /(?i).*[.-]${qualifier}[.\d-]*/
            }
            if (rejected) {
                selection.reject('Release candidate')
            }
        }
    }
}

// https://jeremylong.github.io/DependencyCheck/dependency-check-gradle/configuration.html
dependencyCheck {
    // Specifies if the build should be failed if a CVSS score above a specified level is identified.
    // range of 0-10 fails the build, anything greater and it doesn't fail the build
    failBuildOnCVSS = System.getProperty('dependencyCheck.failBuild') == 'true' ? 0 : 11
    suppressionFile = 'dependency-check-suppressions.xml'
    analyzers {
        // Disable scanning of .NET related binaries
        assemblyEnabled = false
    }
}

repositories {

    maven {
        url "https://dl.bintray.com/hmcts/hmcts-maven"
    }

    maven {
        url 'https://repo.spring.io/libs-milestone'
    }

    jcenter()
    mavenCentral()

    // jitpack should be last resort
    // see: https://github.com/jitpack/jitpack.io/issues/1939
    maven { url 'https://jitpack.io' }
    mavenLocal()
}

project.tasks['sonarqube'].dependsOn test, integration
check.dependsOn integration
checkstyleMain.shouldRunAfter(compileJava)
test.shouldRunAfter(checkstyleTest)
integration.shouldRunAfter(checkstyleIntegrationTest)
integration.shouldRunAfter(test)

sonarqube {
    properties {
        property "sonar.projectName", "SSCS - Tribunals Case Api"
        property "sonar.projectKey", "SSCSTCA"
        property "sonar.jacoco.reportPath", "${project.buildDir}/jacoco/test.exec"
        property "sonar.jacoco.itReportPath", "${project.buildDir}/jacoco/integration.exec"
        property "sonar.exclusions", "src/main/java/uk/gov/hmcts/reform/sscs/domain/**," +
                "src/main/java/uk/gov/hmcts/reform/sscs/TribunalsCaseApiApplication.java," +
                "src/main/java/uk/gov/hmcts/reform/sscs/email/Email.java," +
                "src/main/java/uk/gov/hmcts/reform/sscs/config/*," +
                "src/main/java/uk/gov/hmcts/reform/sscs/model/**"
        property "sonar.pitest.mode", "reuseReport"
        property "sonar.pitest.reportsDirectory", "build/reports/pitest"
    }
}

configurations {
    integrationTestCompile.extendsFrom(testCompile)
    integrationTestRuntime.extendsFrom(testRuntime)
    e2eCompile.extendsFrom(testCompile)
    e2eRuntime.extendsFrom(testRuntime)
}

def springBootVersion = '2.0.5.RELEASE'

def versions = [
        reformLogging: '3.0.1',
        springBoot: springBootVersion,
        springfoxSwagger: '2.8.0',
        pitest: '1.3.2',
        gradlePitest: '1.3.0',
        sonarPitest: '0.5'
]

ext {
    springCloudVersion = 'Finchley.SR1'
}

dependencyManagement {
    imports {
        mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
    }
    dependencies {
        // checkstyle needs a certain version and so does spring fox swagger, so can't use latest
        // fun times...
        dependencySet(group: 'com.google.guava', version: '20.0') {
            entry 'guava'
        }
    }
}

dependencies {

    // Added these due to vulnerability https://nvd.nist.gov/vuln/detail/CVE-2018-8088
    // Remove when springboot has been upgraded to a later version of slf4j
    compile group: 'org.slf4j', name: 'slf4j-api', version: '1.8.0-beta2'
    compile group: 'org.slf4j', name: 'jul-to-slf4j', version: '1.8.0-beta2'
    
    compile group: 'org.springframework.boot', name: 'spring-boot-starter'
    compile group: 'org.springframework.boot', name: 'spring-boot-starter-web'
    compile group: 'org.springframework.boot', name: 'spring-boot-starter-actuator'
    compile group: 'org.springframework.boot', name: 'spring-boot-starter-aop'
    compile group: 'org.springframework.cloud', name: 'spring-cloud-starter-openfeign'
    compile group: 'org.springframework.retry', name: 'spring-retry', version: '1.2.2.RELEASE'

    compile group: 'com.fasterxml.jackson.core', name: 'jackson-databind'
    compile group: 'com.fasterxml.jackson.module', name: 'jackson-module-parameter-names'
    compile group: 'com.fasterxml.jackson.datatype', name: 'jackson-datatype-jdk8'
    compile group: 'com.fasterxml.jackson.datatype', name: 'jackson-datatype-jsr310'

    compile group: 'commons-io', name: 'commons-io', version: '2.6'
    compile group: 'org.apache.commons', name: 'commons-text', version: '1.6'

    compile group: 'org.json', name: 'json', version: '20180813'

    compile group: 'net.objectlab.kit', name: 'datecalc-common', version: '1.4.0'
    compile group: 'net.objectlab.kit', name: 'datecalc-jdk8', version: '1.4.0'

    compile group: 'io.springfox', name: 'springfox-swagger2', version: versions.springfoxSwagger
    compile group: 'io.springfox', name: 'springfox-swagger-ui', version: versions.springfoxSwagger

    compile 'com.migesok:jaxb-java-time-adapters:1.1.3'

    compile group: 'io.github.openfeign', name: 'feign-httpclient', version: '10.0.1'

    compile group: 'uk.gov.hmcts.reform', name: 'java-logging-spring', version: versions.reformLogging
    compile group: 'uk.gov.hmcts.reform', name: 'java-logging-httpcomponents', version: versions.reformLogging
    compile group: 'uk.gov.hmcts.reform', name: 'java-logging-appinsights', version: versions.reformLogging
    compile group: 'uk.gov.hmcts.reform', name: 'http-proxy-spring-boot-autoconfigure', version: '1.1.0'
    compile group: 'uk.gov.hmcts.reform', name: 'service-auth-provider-client', version: '1.0.0'
    compile group: 'uk.gov.hmcts.reform', name: 'core-case-data-store-client', version: '4.2.0'
    compile group: 'uk.gov.hmcts.reform', name: 'sscs-common', version: '0.0.123'
    compile group: 'uk.gov.hmcts.reform', name: 'sscs-pdf-email-common', version: '0.0.96'
    compile group: 'uk.gov.hmcts.reform', name: 'sscs-robotics-common', version: '0.0.3'

    compile group: 'io.rest-assured', name: 'rest-assured', version: '3.1.1'

    compile group: 'com.github.sps.junidecode', name: 'junidecode', version: '0.3'

    compileOnly 'org.projectlombok:lombok:1.18.2'
    apt "org.projectlombok:lombok:1.16.20"

    testCompile group: 'org.springframework.boot', name: 'spring-boot-starter-test', version: '1.5.10.RELEASE'
    testCompile group: 'org.mockito', name: 'mockito-core', version: '2.22.0'
    testCompile group: 'org.hamcrest', name: 'hamcrest-core', version: '1.4-atlassian-1'
    testCompile group: 'com.github.tomakehurst', name: 'wiremock', version: '2.19.0'
    testCompile group: 'net.javacrumbs.json-unit', name: 'json-unit', version: '1.31.1'

    testCompileOnly 'org.projectlombok:lombok:1.16.20'
    testApt "org.projectlombok:lombok:1.16.20"

    runtime "org.springframework.boot:spring-boot-properties-migrator"
    compile group: 'org.pitest', name: 'pitest', version: '1.4.3'
    compile 'info.solidsoft.gradle.pitest:gradle-pitest-plugin:1.3.0'
    compile 'org.codehaus.sonar-plugins:sonar-pitest-plugin:0.5'
}

// this is required to force Java running on the Azure Windows Server OS into using
// UTF-8 as the default character set instead of windows-1252 which causes issues
applicationDefaultJvmArgs = ["-Dfile.encoding=UTF-8"]

bootJar {
    archiveName 'tribunals-case-api.jar'
    manifest {
        attributes 'Implementation-Title': project.name,
                'Implementation-Version': project.version
    }
}

jacocoTestReport {
    executionData(test, integration)
    reports {
        xml.enabled = true
        csv.enabled = false
    }
}

pitest {
    targetClasses = ['uk.gov.hmcts.sscs.*']
    excludedClasses = ['uk.gov.hmcts.reform.sscs.config.*','uk.gov.hmcts.reform.sscs.models.*','uk.gov.hmcts.reform.sscs.TribunalsCaseApiApplication.java']
    threads = 10
    outputFormats = ['XML', 'HTML']
    timestampedReports = false
    mutationThreshold = 50
}

task smoke(type: Test) {
    group = 'verification'
    description = 'Executes non-destructive smoke tests against a running Tribual Case API'
    setTestClassesDirs(sourceSets.e2e.output.classesDirs)
    setClasspath(sourceSets.e2e.runtimeClasspath)
    include "uk/gov/hmcts/reform/sscs/smoke/**"
    exclude "uk/gov/hmcts/reform/sscs/functional/**"
}
