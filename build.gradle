import uk.gov.hmcts.rse.AuthMode
import java.nio.charset.StandardCharsets

plugins {
    id 'application'
    id 'pmd'
    id 'org.sonarqube' version '6.2.0.5505'
    id 'jacoco'
    id 'org.springframework.boot' version '3.5.6'
    id 'uk.gov.hmcts.java' version '0.12.67'
    id 'io.spring.dependency-management' version '1.1.7'
    id 'com.gorylenko.gradle-git-properties' version '2.5.0'
    id 'com.github.ben-manes.versions' version '0.52.0'
    id 'org.owasp.dependencycheck' version '12.1.3'
    id 'au.com.dius.pact' version '4.6.17'
    id 'com.github.hmcts.rse-cft-lib' version '0.19.1856'
    id 'info.solidsoft.pitest' version '1.15.0'
}

apply plugin: 'org.springframework.boot'
apply plugin: 'io.spring.dependency-management'

gitProperties {
    gitPropertiesDir = new File("${project.rootDir}/src/main/resources/uk/gov/hmcts/reform/sscs")
    keys = ['git.commit.id', 'git.commit.time']
    dateFormat = "yyyy-MM-dd'T'HH:mmZ"
    dateFormatTimeZone = "GMT"
}


group = 'uk.gov.hmcts.reform'
version = '1.0.0'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

compileJava {
    options.compilerArgs << '-parameters' << '-Xlint:deprecation' << "-Xlint:unchecked"
}

compileTestJava {
    options.compilerArgs << '-Xlint:deprecation'
}

tasks.withType(Copy).configureEach { duplicatesStrategy DuplicatesStrategy.WARN }

application {
    mainClass.set('uk.gov.hmcts.reform.sscs.TribunalsCaseApiApplication')
    // this is required to force Java running on the Azure Windows Server OS into using
    // UTF-8 as the default character set instead of windows-1252 which causes issues
    applicationDefaultJvmArgs = ["-Dfile.encoding=UTF-8"]
}

pmd {
    toolVersion = "7.13.0"
    ignoreFailures = true
    sourceSets = [sourceSets.main, sourceSets.test]
    reportsDir = layout.buildDirectory.dir("reports/pmd").get().asFile
    ruleSetFiles = files("config/pmd/ruleset.xml")
}

sourceSets {
    integrationTest {
        java {
            compileClasspath += main.output + test.output
            runtimeClasspath += main.output + test.output
            srcDir('src/IntegrationTests/java')
        }
        resources {
            srcDir('src/IntegrationTests/resources')
        }
    }
    functionalTest {
        java {
            compileClasspath += main.output + test.output
            runtimeClasspath += main.output + test.output
            srcDir('src/functionalTest/java')
        }
    }
    contractTest {
        java {
            compileClasspath += main.output
            runtimeClasspath += main.output
            srcDir file('src/contractTest/java')
        }
        resources.srcDir file('src/contractTest/resources')
    }
}

dependencies {
    integrationTestImplementation(sourceSets.test.output)
    functionalTestImplementation(sourceSets.test.output)
}

tasks.withType(Test).configureEach {
    useJUnitPlatform()

    testLogging {
        exceptionFormat = 'full'
    }
}

tasks.register('integration', Test) {
    jvmArgs = ['-Xms1024m', '-Xmx2048m']
    maxParallelForks = Runtime.runtime.availableProcessors().intdiv(2) ?: 1
    forkEvery = 10
    setTestClassesDirs(sourceSets.integrationTest.output.classesDirs)
    setClasspath(sourceSets.integrationTest.runtimeClasspath)
}

tasks.register('functional', Test) {
    group = 'Functional Tests'
    description = 'Executes non-destructive functional tests in AAT against a running CCD and notification service'
    setTestClassesDirs(sourceSets.functionalTest.output.classesDirs)
    setClasspath(sourceSets.functionalTest.runtimeClasspath)
    include "uk/gov/hmcts/reform/sscs/functional/**"
    include "uk/gov/hmcts/reform/sscs/controllers/**"
    include "uk/gov/hmcts/reform/sscs/bulkscan/**"
    exclude "uk/gov/hmcts/reform/sscs/smoke/**"
    maxParallelForks = Runtime.runtime.availableProcessors()
}

tasks.register('verifyNotifications', Test) {
    group = 'Notifications verification Tests'
    description = 'Simulates CCD callbacks to generate notifications, then retrieves pdfs from Gov.Notify'
    setTestClassesDirs(sourceSets.functionalTest.output.classesDirs)
    setClasspath(sourceSets.functionalTest.runtimeClasspath)
    include "uk/gov/hmcts/reform/sscs/notifications/**"
    exclude "uk/gov/hmcts/reform/sscs/smoke/**"
    maxParallelForks = Runtime.runtime.availableProcessors()
}

tasks.register('smoke', Test) {
    group = 'verification'
    description = 'Executes non-destructive smoke tests against a running Tribunal Case API'
    setTestClassesDirs(sourceSets.functionalTest.output.classesDirs)
    setClasspath(sourceSets.functionalTest.runtimeClasspath)
    include "uk/gov/hmcts/reform/sscs/smoke/**"
    exclude "uk/gov/hmcts/reform/sscs/functional/**"
}

tasks.register('contract', Test) {
    description = "Runs the consumer Pact tests"
    group = 'Verification'
    useJUnitPlatform()
    testClassesDirs = sourceSets.contractTest.output.classesDirs
    classpath = sourceSets.contractTest.runtimeClasspath
}

tasks.register('fortifyScan', JavaExec) {
    mainClass = "uk.gov.hmcts.fortifyclient.FortifyClientMainApp"
    classpath += sourceSets.test.runtimeClasspath
    jvmArgs = ['--add-opens=java.base/java.lang.reflect=ALL-UNNAMED']
}

jacoco {
    toolVersion = "0.8.13"
}

jacocoTestReport {
    executionData(test, integration)
    reports {
        xml.required = true
        csv.required = false
        xml.outputLocation = layout.buildDirectory.file("reports/jacoco/test/jacocoTestReport.xml")
    }
}

dependencyUpdates.resolutionStrategy = {
    componentSelection { rules ->
        rules.all {
            def isNonStable = { String version ->
                def stableKeyword = ['RELEASE', 'FINAL', 'GA'].any { qualifier -> version.toUpperCase().contains(qualifier) }
                def regex = /^[0-9,.v-]+(-r)?$/
                return !stableKeyword && !(version ==~ regex)
            }
            if (isNonStable(candidate.version) && !isNonStable(currentVersion)) {
                selection.reject('Release candidate')
            }
        }
    }
}

// https://jeremylong.github.io/DependencyCheck/dependency-check-gradle/configuration.html
dependencyCheck {
    // Specifies if the build should be failed if a CVSS score above a specified level is identified.
    // range of 7-10 fails the build, anything lower and it doesn't fail the build
    failBuildOnCVSS = System.getProperty('dependencyCheck.failBuild', 'true') == 'true' ? 7.0 : 11
    suppressionFile = 'config/owasp/suppressions.xml'
    nvd.validForHours = 24
    analyzers.retirejs.enabled = false
    analyzers.ossIndex.enabled = false
}

repositories {

    mavenLocal()
    mavenCentral()
    maven {
        url 'https://pkgs.dev.azure.com/hmcts/Artifacts/_packaging/hmcts-lib/maven/v1'
    }
    // Requires authentication, if it gets here the library cannot be found!
    maven {
        url 'https://repo.spring.io/libs-milestone'
    }
}

project.tasks.named('pitest') {
    group = "Verification"
}

pitest {
    targetClasses = ['uk.gov.hmcts.reform.sscs.*']
    excludedClasses = [
            'uk.gov.hmcts.reform.sscs.config.*',
            'uk.gov.hmcts.reform.sscs.bulkscancore.domain.*',
            'uk.gov.hmcts.reform.sscs.domain.*',
            'uk.gov.hmcts.reform.sscs.TribunalsCaseApiApplication'
    ]
    threads = 10
    outputFormats = ['XML', 'HTML']
    timestampedReports = false
    mutationThreshold = 90
}

processResources.dependsOn(generateGitProperties)

project.tasks.named('sonarqube') {
    dependsOn test, jacocoTestReport
}

checkstyleMain.shouldRunAfter(compileJava)
test.shouldRunAfter(checkstyleTest)

sonarqube {
    properties {
        property "sonar.projectName", "SSCS - Tribunals Case Api"
        property "sonar.projectKey", "SSCSTCA"
        property "sonar.coverage.jacoco.xmlReportPaths", "${jacocoTestReport.reports.xml.outputLocation}"
        property "sonar.exclusions", "**/CreateCaseException.java," +
                "src/main/java/uk/gov/hmcts/reform/sscs/domain/**," +
                "src/main/java/uk/gov/hmcts/reform/sscs/evidenceshare/domain/**," +
                "src/main/java/uk/gov/hmcts/reform/sscs/TribunalsCaseApiApplication.java," +
                "src/main/java/uk/gov/hmcts/reform/sscs/email/Email.java," +
                "src/main/java/uk/gov/hmcts/reform/sscs/config/**/*.java," +
                "src/main/java/uk/gov/hmcts/reform/sscs/model/**/*.java," +
                "src/main/java/uk/gov/hmcts/reform/sscs/evidenceshare/model/**/*.java," +
                "src/main/java/uk/gov/hmcts/reform/sscs/service/EvidenceUploadService.java," +
                "src/main/java/uk/gov/hmcts/reform/sscs/service/OnlineHearingService.java," +
                "src/main/java/uk/gov/hmcts/reform/sscs/service/pdf/data/EvidenceDescriptionPdfData.java," +
                "src/main/java/uk/gov/hmcts/reform/sscs/service/pdf/StoreAppellantStatementService.java," +
                "src/main/java/uk/gov/hmcts/reform/sscs/service/pdf/StorePdfService.java," +
                "src/main/java/uk/gov/hmcts/reform/sscs/service/servicebus/SessionAwareServiceBusMessagingService.java," +
                "src/main/java/uk/gov/hmcts/reform/sscs/service/coversheet/PdfCoverSheet.java," +
                "src/main/java/uk/gov/hmcts/reform/sscs/service/HmcHearingApiService.java," +
                "src/main/java/uk/gov/hmcts/reform/sscs/controllers/CreateCaseController.java," +
                "src/main/java/uk/gov/hmcts/reform/sscs/service/pdf/data/PdfData.java," +
                "src/main/java/uk/gov/hmcts/reform/sscs/ccd/presubmit/writefinaldecision/esa/EsaWriteFinalDecisionMidEventValidationHandler.java," +
                "src/main/java/uk/gov/hmcts/reform/sscs/evidenceshare/config/**," +
                "src/main/java/uk/gov/hmcts/reform/sscs/service/servicebus/messaging/**," +
                "src/main/java/uk/gov/hmcts/reform/sscs/docmosis/domain/**," +
                "src/main/java/uk/gov/hmcts/reform/sscs/docmosis/config/**," +
                "src/main/java/uk/gov/hmcts/reform/sscs/EnvironmentPropertiesPrinter.java," +
                "src/main/java/uk/gov/hmcts/reform/sscs/controller/FunctionalTestController.java," +
                "src/main/java/uk/gov/hmcts/reform/sscs/tyanotifications/utils/GetAllTemplatesFromNotify.java," +
                "src/main/java/uk/gov/hmcts/reform/sscs/bulkscan/domain/**," +
                "src/main/java/uk/gov/hmcts/reform/sscs/bulkscan/bulkscancore/domain/**,"
        property "sonar.host.url", "https://sonar.reform.hmcts.net/"
    }
}

configurations {
    integrationTestImplementation.extendsFrom(testImplementation)
    integrationTestRuntimeOnly.extendsFrom(testRuntimeOnly)
    functionalTestImplementation.extendsFrom(testImplementation)
    functionalTestRuntimeOnly.extendsFrom(testRuntimeOnly)
    contractTestImplementation.extendsFrom(testImplementation)
    contractTestRuntimeOnly.extendsFrom(testRuntimeOnly)
}

tasks.register('installLocalGitHook', Copy) {
    from new File(rootProject.rootDir, 'config/git/pre-commit')
    into { new File(rootProject.rootDir, '.git/hooks') }
    fileMode 0775
}

generateGitProperties.dependsOn installLocalGitHook
compileJava.dependsOn installLocalGitHook

tasks.register('runGitPreCommitTasks') {
    dependsOn 'pmdMain'
    dependsOn 'pmdTest'
    dependsOn 'checkstyleMain'
    dependsOn 'checkstyleTest'
}

dependencyManagement {
    dependencies {
        imports {
            mavenBom 'org.springframework.cloud:spring-cloud-dependencies:2025.0.0'
        }
    }
}

def versions = [
        springboot     : springBoot.class.package.implementationVersion,
        springsecurity : '6.5.5',
        jackson        : '2.19.0',
        lombok         : '1.18.42',
        commonsio      : '2.21.0',
        openfeign      : '13.6'
]

dependencies {
    implementation group: 'org.springframework.boot', name: 'spring-boot-autoconfigure', version: versions.springboot
    implementation group: 'org.springframework.boot', name: 'spring-boot-starter', version: versions.springboot
    implementation group: 'org.springframework.boot', name: 'spring-boot-starter-activemq', version: versions.springboot
    implementation group: 'org.springframework.boot', name: 'spring-boot-starter-actuator', version: versions.springboot
    implementation group: 'org.springframework.boot', name: 'spring-boot-starter-aop', version: versions.springboot
    implementation group: 'org.springframework.boot', name: 'spring-boot-starter-jdbc', version: versions.springboot
    implementation group: 'org.springframework.boot', name: 'spring-boot-starter-mail', version: versions.springboot
    implementation group: 'org.springframework.boot', name: 'spring-boot-starter-security', version: versions.springboot
    implementation group: 'org.springframework.boot', name: 'spring-boot-starter-validation', version: versions.springboot
    implementation group: 'org.springframework.boot', name: 'spring-boot-starter-web', version: versions.springboot

    implementation group: 'org.springframework.security', name: 'spring-security-config', version: versions.springsecurity
    implementation group: 'org.springframework.security', name: 'spring-security-core', version: versions.springsecurity
    implementation group: 'org.springframework.security', name: 'spring-security-crypto', version: versions.springsecurity
    implementation group: 'org.springframework.security', name: 'spring-security-web', version: versions.springsecurity

    implementation group: 'org.springframework', name: 'spring-jms'
    implementation group: 'org.springframework', name: 'spring-context-support'
    implementation group: 'org.springframework', name: 'spring-tx'
    implementation group: 'org.springframework.cloud', name: 'spring-cloud-starter-openfeign'
    implementation group: 'org.springframework.retry', name: 'spring-retry', version: '2.0.12'

    implementation group: 'io.github.openfeign', name: 'feign-hc5', version: versions.openfeign
    implementation group: 'io.github.openfeign', name: 'feign-httpclient', version: versions.openfeign
    implementation group: 'io.github.openfeign', name: 'feign-jackson', version: versions.openfeign

    implementation group: 'com.fasterxml.jackson.core', name: 'jackson-annotations', version: versions.jackson
    implementation group: 'com.fasterxml.jackson.core', name: 'jackson-core', version: versions.jackson
    implementation group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: versions.jackson
    implementation group: 'com.fasterxml.jackson.datatype', name: 'jackson-datatype-jdk8', version: versions.jackson
    implementation group: 'com.fasterxml.jackson.datatype', name: 'jackson-datatype-jsr310', version: versions.jackson

    implementation group: 'com.networknt', name: 'json-schema-validator', version: '1.5.7'
    implementation group: 'org.json', name: 'json', version: '20250517'

    implementation group: 'com.github.hmcts', name: 'auth-checker-lib', version: '3.1.1'
    implementation group: 'com.github.hmcts', name: 'befta-fw', version: '9.2.4', {
        exclude group: 'org.apache.commons', module: 'commons-compress'
        exclude group: 'com.google.guava', module: 'guava'
        exclude group: 'org.apache.poi', module: 'poi-ooxml'
        exclude group: 'org.apache.poi', module: 'poi'
        exclude group: 'com.launchdarkly', module: 'launchdarkly-java-server-sdk'
        exclude group: 'org.codehaus.groovy', module: 'groovy-xml'
    }
    implementation group: 'com.github.hmcts', name: 'ccd-case-document-am-client', version: '1.59.2'
    implementation group: 'com.github.hmcts', name: 'core-case-data-store-client', version: '5.1.0'
    implementation group: 'com.github.hmcts', name: 'doc-assembly-client', version: '2.2.0'
    //remove when secure doc store is live
    implementation group: 'com.github.hmcts', name: 'document-management-client', version: '7.0.1'
    implementation group: 'com.github.hmcts', name: 'idam-java-client', version: '3.0.4'
    implementation group: 'com.github.hmcts', name: 'pdf-service-client', version: '8.0.2'
    implementation group: 'com.github.hmcts', name: 'send-letter-client', version: '5.1.1'
    implementation group: 'com.github.hmcts', name: 'service-auth-provider-java-client', version: '5.3.3'

    implementation group: 'com.github.hmcts', name: 'sscs-common', version: '6.3.18'

    implementation group: 'com.github.hmcts.java-logging', name: 'logging', version: '6.1.9'

    implementation group: 'uk.gov.service.notify', name: 'notifications-java-client', version: '5.2.1-RELEASE'

    implementation group: 'com.azure', name: 'azure-core', version: '1.55.3'
    implementation group: 'com.azure', name: 'azure-messaging-servicebus', version: '7.17.11'
    implementation group: 'com.azure', name: 'azure-servicebus-jms', version: '2.0.0'
    implementation group: 'com.microsoft.azure', name: 'applicationinsights-web', version: '3.7.2'

    implementation group: 'jakarta.inject', name: 'jakarta.inject-api', version: '2.0.1'
    implementation group: 'javax.annotation', name: 'javax.annotation-api', version: '1.3.2'
    implementation group: 'javax.validation', name: 'validation-api', version: '2.0.1.Final'
    implementation group: 'javax.xml.bind', name: 'jaxb-api', version: '2.3.1'

    implementation group: 'com.mchange', name: 'c3p0', version: '0.11.2'
    implementation group: 'org.flywaydb', name: 'flyway-core', version: '11.8.2'
    implementation group: 'org.flywaydb', name: 'flyway-database-postgresql', version: '11.8.2'
    implementation group: 'org.postgresql', name: 'postgresql', version: '42.7.7'
    implementation group: 'org.quartz-scheduler', name: 'quartz', version: '2.5.0'

    implementation group: 'io.projectreactor', name: 'reactor-core', version: '3.7.6'
    implementation group: 'org.elasticsearch', name: 'elasticsearch', version: '9.0.3'

    implementation group: 'com.squareup.okhttp3', name: 'okhttp', version: '4.12.0'
    implementation group: 'com.squareup.okio', name: 'okio-jvm', version: '3.16.0'
    implementation group: 'commons-io', name: 'commons-io', version:  versions.commonsio

    implementation group: 'net.logstash.logback', name: 'logstash-logback-encoder', version: '8.1'
    implementation group: 'org.springdoc', name: 'springdoc-openapi-starter-webmvc-ui', version: '2.8.8'

    implementation group: 'com.google.guava', name: 'guava', version: '33.5.0-jre'
    implementation group: 'com.opencsv', name: 'opencsv', version: '5.11'
    implementation group: 'net.objectlab.kit', name: 'datecalc-jdk8', version: '1.4.8'
    implementation group: 'org.commonmark', name: 'commonmark', version: '0.24.0'
    implementation group: 'org.overviewproject', name: 'mime-types', version: '2.0.0'
    implementation group: 'pl.touk', name: 'throwing-function', version: '1.3'

    implementation group: 'com.github.sps.junidecode', name: 'junidecode', version: '0.3'
    implementation group: 'com.twelvemonkeys.imageio', name: 'imageio-tiff', version: '3.12.0'
    implementation group: 'net.coobird', name: 'thumbnailator', version: '0.4.20'

    implementation group: 'org.apache.commons', name: 'commons-collections4', version: '4.5.0'
    implementation group: 'org.apache.commons', name: 'commons-text', version: '1.13.1'
    implementation group: 'org.apache.pdfbox', name: 'pdfbox', version: '3.0.5'
    implementation group: 'org.apache.pdfbox', name: 'xmpbox', version: '3.0.5'
    implementation group: 'org.apache.tika', name: 'tika-core', version: '3.2.3'
    implementation group: 'org.apache.tika', name: 'tika-parsers', version: '3.2.3'
    implementation group: 'org.apache.tomcat.embed', name: 'tomcat-embed-core', version: '11.0.8'

    annotationProcessor group: 'org.projectlombok', name: 'lombok', version: versions.lombok
    implementation group: 'org.projectlombok', name: 'lombok', version: versions.lombok
    implementation group: 'com.launchdarkly', name: 'launchdarkly-java-server-sdk', version: '7.12.0'
    implementation group: 'me.xdrop', name: 'fuzzywuzzy', version: '1.4.0'

    contractTestImplementation sourceSets.main.runtimeClasspath
    contractTestImplementation sourceSets.test.runtimeClasspath

    cftlibImplementation group: 'org.springframework.boot', name: 'spring-boot-devtools', version: versions.springboot
    testImplementation group: 'org.springframework.boot', name: 'spring-boot-starter-quartz', version: versions.springboot
    contractTestImplementation group: 'org.springframework.boot', name: 'spring-boot-starter-test', version: versions.springboot
    testImplementation group: 'org.springframework.boot', name: 'spring-boot-starter-test', version: versions.springboot, {
        exclude group: 'com.vaadin.external.google', module: 'android-json'
    }

    testImplementation group: 'io.rest-assured', name: 'json-path', version: '5.5.5'
    testImplementation group: 'io.rest-assured', name: 'rest-assured', version: '5.5.5'
    testImplementation group: 'io.rest-assured', name: 'xml-path', version: '5.5.5'

    testImplementation group: 'com.github.hmcts', name: 'fortify-client', version: '1.4.10', classifier: 'all', {
        exclude group: 'commons-io', module: 'commons-io'
    }

    testImplementation group: 'org.apache.httpcomponents.client5', name: 'httpclient5-fluent', version: '5.5'

    testImplementation group: 'net.javacrumbs.json-unit', name: 'json-unit', version: '4.1.1'
    testImplementation group: 'net.javacrumbs.json-unit', name: 'json-unit-assertj', version: '4.1.1'
    testImplementation group: 'org.assertj', name: 'assertj-core', version: '3.27.3'
    testImplementation group: 'org.awaitility', name: 'awaitility', version: '4.3.0'
    testImplementation group: 'org.mockito', name: 'mockito-core', version: '5.18.0'
    testImplementation group: 'org.mockito', name: 'mockito-junit-jupiter', version: '5.18.0'
    testImplementation group: 'com.github.tomakehurst', name: 'wiremock-standalone', version: '3.0.1'
    testImplementation group: 'org.wiremock.integrations', name: 'wiremock-spring-boot', version: '3.10.0'
    testImplementation group: 'pl.pragmatists', name: 'JUnitParams', version: '1.1.1'

    testImplementation group: 'org.apiguardian', name: 'apiguardian-api', version: '1.1.2'
    testImplementation platform(group: 'org.junit', name: 'junit-bom', version: '5.13.4')
    testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter'
    testImplementation group: 'org.junit.vintage', name: 'junit-vintage-engine'
    testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-api'
    testRuntimeOnly group: 'org.junit.jupiter', name: 'junit-jupiter-engine'
    contractTestImplementation group: 'au.com.dius.pact.consumer', name: 'junit5', version: '4.6.17'
    contractTestRuntimeOnly group: 'au.com.dius.pact.consumer', name: 'junit5', version: '4.6.17'

    testImplementation group: 'info.solidsoft.gradle.pitest', name: 'gradle-pitest-plugin', version: pitest.pitestVersion.get()
    testImplementation group: 'org.codehaus.sonar-plugins', name: 'sonar-pitest-plugin', version: '0.5'
    testImplementation group: 'org.pitest', name: 'pitest', version: '1.20.1'
    testImplementation group: 'com.typesafe', name: 'config', version: '1.4.3'

    testImplementation group: 'com.h2database', name: 'h2', version: '2.3.232'
    testImplementation group: 'io.github.artsok', name: 'rerunner-jupiter', version: '2.1.6'

    cftlibAnnotationProcessor group: 'org.projectlombok', name: 'lombok', version: versions.lombok
    functionalTestAnnotationProcessor group: 'org.projectlombok', name: 'lombok', version: versions.lombok
    functionalTestImplementation group: 'org.projectlombok', name: 'lombok', version: versions.lombok
    integrationTestAnnotationProcessor group: 'org.projectlombok', name: 'lombok', version: versions.lombok
    testAnnotationProcessor group: 'org.projectlombok', name: 'lombok', version: versions.lombok
    testImplementation group: 'org.projectlombok', name: 'lombok', version: versions.lombok

    integrationTestImplementation sourceSets.main.runtimeClasspath
    integrationTestImplementation sourceSets.test.runtimeClasspath

}

bootJar {
    archiveFileName = 'tribunals-case-api.jar'
    manifest {
        attributes 'Implementation-Title': project.name,
                'Implementation-Version': project.version
    }
}

project.ext {
    pacticipant = 'sscs_tribunalsCaseApi'
    pacticipantVersion = getCheckedOutGitCommitHash()
}

tasks.register('runAndPublishConsumerPactTests', Test) {
    logger.lifecycle("Runs pact Tests")
    testClassesDirs = sourceSets.contractTest.output.classesDirs
    classpath = sourceSets.contractTest.runtimeClasspath
}

pact {
    broker {
        pactBrokerUrl = System.getenv("PACT_BROKER_FULL_URL") ?: 'http://localhost:80'
    }
    publish {
        pactDirectory = 'pacts'
        tags = [System.getenv("PACT_BRANCH_NAME") ?:'Dev']
        version = project.pacticipantVersion
    }
}

runAndPublishConsumerPactTests.dependsOn contract

runAndPublishConsumerPactTests.finalizedBy pactPublish


static def getCheckedOutGitCommitHash() {
    'git rev-parse --verify --short HEAD'.execute().text.trim()
}

run {
    def debug = System.getProperty("debug")
    if (debug == 'true') {
        jvmArgs = ['-agentlib:jdwp=transport=dt_socket,server=y,suspend=y,address=5006']
    }
}


tasks.register('generateXls', Exec) {
    commandLine project.file('./bin/create-xlsx.sh').path, 'benefit', 'dev', 'local'
}

bootWithCCD {
    group 'application'
    dependsOn generateXls
    authMode = AuthMode.Local

    doFirst() {
        if (!project.file('./.aat-env').exists()) {
            new ByteArrayOutputStream().withStream { os ->
                exec {
                    commandLine 'az', 'keyvault', 'secret', 'show', '--vault-name', 'sscs-aat', '-o', 'tsv', '--query', 'value', '--name', 'sscs-tribunals-case-api-dot-env'
                    standardOutput = os
                }
                project.file('./.aat-env').write(new String(os.toString().replace('\n', '').decodeBase64(), StandardCharsets.UTF_8))
            }
        }

        project.file('./.aat-env').readLines().each() {
            def index = it.indexOf("=")
            def key = it.substring(0, index)
            def value = it.substring(index + 1)
            environment key, value
        }
    }

    //---Debug-----
    //Uncomment to launch with debug
    //jvmArgs = ['-agentlib:jdwp=transport=dt_socket,server=y,suspend=y,address=5006']

    //---Extra ccd loging----
    //Uncomment for ccd callback logging
    //environment 'LOG_CALLBACK_DETAILS', '*'

    def fileLogger = [ onOutput: {
        File logfile = new File('gradle-build.log')
        logfile << it
    }
    ] as StandardOutputListener

    logging.addStandardOutputListener(fileLogger)

    gradle.taskGraph.whenReady { taskGraph ->
        taskGraph.allTasks.each { Task t ->
            t.doFirst {
                logging.addStandardOutputListener(fileLogger)
            }
        }
    }
}

tasks.register('yarnInstall', Exec) {
    workingDir = file("${project.projectDir}/definitions/test")
    commandLine 'yarn', 'install'
}

tasks.register('runE2eTests', Exec) {
    dependsOn yarnInstall
    workingDir = file("${project.projectDir}/definitions/test")
    commandLine 'yarn', 'test:' + System.getenv('ENVIRONMENT')
}

tasks.register('runE2eTestsNightly', Exec) {
    dependsOn yarnInstall
    workingDir = file("${project.projectDir}/definitions/test")
    commandLine 'yarn', 'test:nightly'
}

tasks.register('runAccessibilityTestsNightly', Exec) {
    dependsOn yarnInstall
    workingDir = file("${project.projectDir}/definitions/test")
    commandLine 'yarn', 'test:accessibility'
}

tasks.register('highLevelDataSetup', JavaExec) {
    mainClass = "uk.gov.hmcts.reform.sscs.config.HighLevelDataSetupApp"
    classpath += sourceSets.main.runtimeClasspath
    jvmArgs = ['--add-opens=java.base/java.lang.reflect=ALL-UNNAMED']
}

def dbHost = System.env.JOB_SCHEDULER_DB_HOST ?: "localhost"
def dbPort = System.env.JOB_SCHEDULER_DB_PORT ?: 5432
def dbName = System.env.JOB_SCHEDULER_DB_NAME ?: "sscsjobscheduler"
def dbUser = System.env.JOB_SCHEDULER_DB_USERNAME ?: "sscsjobscheduler"
def dbPass = System.env.JOB_SCHEDULER_DB_PASSWORD ?: "sscsjobscheduler"
